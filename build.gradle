plugins {
  id 'application'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'org.owasp.dependencycheck' version '6.2.2'
  id 'org.springframework.boot' version '2.5.3'
  id 'com.github.ben-manes.versions' version '0.36.0'
  id 'org.sonarqube' version '3.2.0'
  id 'info.solidsoft.pitest' version '1.6.0'
  id 'au.com.dius.pact' version '4.2.9'
  id 'org.flywaydb.flyway' version '7.13.0'
}

group = 'uk.gov.hmcts.reform'
version = '0.1.0' // all the time it was 0.0.1. 0.1.0 marks migration to java 11

sourceCompatibility = 11
targetCompatibility = 11

sourceSets {
  contractTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
    }
  }
  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
    }
  }

  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
    }
  }

  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
    }
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

test {
  failFast = true
}

task runAndPublishConsumerPactTests(type: Test) {
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
}

runAndPublishConsumerPactTests.finalizedBy pactPublish

task functional(type: Test, description: 'Runs the functional tests.', group: 'Verification') {
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
}

task smoke(type: Test) {
  description = "Runs Smoke Tests"
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
}

task contract(type: Test) {
  description = "Runs contracts tests"
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
}

task fortifyScan(type: JavaExec)  {
  main = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

checkstyle {
  maxWarnings = 0
  toolVersion = '8.29'
  // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
  configDirectory.set(new File(rootDir, 'config/checkstyle'))
}

pmd {
  toolVersion = "6.14.0"
  ignoreFailures = true
  incrementalAnalysis = true
  sourceSets = [sourceSets.main, sourceSets.test, sourceSets.functionalTest, sourceSets.integrationTest, sourceSets.smokeTest]
  reportsDir = file("$project.buildDir/reports/pmd")
  ruleSetFiles = files("config/pmd/ruleset.xml")
}

jacocoTestReport {
  dependsOn test, integration
  executionData(test, integration)
  reports {
    xml.enabled = true
    csv.enabled = false
    html.destination file("${buildDir}/reports/jacoco/html")
    xml.destination file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
  afterEvaluate {
    getClassDirectories().from = getClassDirectories().files.collect {
      fileTree(dir: it, exclude: [
        'uk/gov/hmcts/reform/bulkscan/orchestrator/config/**'
      ])
    }
  }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport

pitest {
  group = "Verification"
  pitestVersion.set('1.5.1')
  junit5PluginVersion.set('0.12')
  testSourceSets = [sourceSets.test]
  mainSourceSets = [sourceSets.main]
  targetClasses = ['uk.gov.hmcts.reform.bulkscan.orchestrator.*']
  excludedClasses = [
    'uk.gov.hmcts.reform.bulkscan.orchestrator.config.*'
  ]
  threads = 10
  enableDefaultIncrementalAnalysis = true
  outputFormats = ['XML', 'HTML']
  timestampedReports = false
  mutationThreshold = 50
  timeoutConstInMillis = 30000
  mutators.set([
    'CONDITIONALS_BOUNDARY',
    'INCREMENTS',
    'INVERT_NEGS',
    'MATH',
    'NEGATE_CONDITIONALS',
    'VOID_METHOD_CALLS',
    'EMPTY_RETURNS',
    'FALSE_RETURNS',
    'NULL_RETURNS',
    'PRIMITIVE_RETURNS',
    'TRUE_RETURNS',
    'REMOVE_CONDITIONALS'
  ])
}

sonarqube {
  properties {
    property "sonar.projectName", "Reform :: Bulk Scan Orchestrator"
    property "sonar.coverage.jacoco.xmlReportPaths", jacocoTestReport.reports.xml.destination.path
    // TODO: remove the specific exclusions when those classes are used
    property "sonar.coverage.exclusions", "**/config/**"
    property "sonar.cpd.exclusions", "**/caseupdate/model/request/ExceptionRecord.java,**/TransformationRequest.java,**/internal/ExceptionRecord.java"
  }
}

pact {
  publish {
    pactBrokerUrl = 'https://pact-broker.platform.hmcts.net'
    version = '0.0.1'
  }
}

// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'config/owasp/suppressions.xml'
  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
}

repositories {
  mavenLocal()
  jcenter()
  maven {
    url 'https://dl.bintray.com/hmcts/hmcts-maven'
  }
  maven { url 'https://jitpack.io' }
}

// it is important to specify logback classic and core packages explicitly as libraries like spring boot
// enforces it's own (older) version which is not recommended.
def versions = [
  reformLogging     : '5.1.6',
  springfoxSwagger  : '2.9.2',
  junit             : '5.6.2',
  junitPlatform     : '1.6.2',
]

ext["rest-assured.version"] = '4.3.0'

dependencyManagement {
  dependencies {
    // CVE-2020-8908
    dependencySet(group: 'com.google.guava', version: '30.1.1-jre') {
      entry 'guava'
    }
    // force junit5 deps to use groovy v3 which fixes reflective call errors for java 11
    // rest assured 4.2 -> 4.3 jumps to groovy v3. junit v5.6 still on v2.5
    dependencySet(group: 'org.codehaus.groovy', version: '3.0.8') {
      entry 'groovy'
      entry 'groovy-json'
      entry 'groovy-xml'
    }
    // avoid CVE-2021-29425
    dependencySet(group: 'commons-io', version: '2.11.0') {
      entry 'commons-io'
    }
    // avoid CVE-2021-27568
    dependencySet(group: 'com.nimbusds', version: '9.11') {
      entry 'nimbus-jose-jwt'
    }
  }
}

dependencies {
  implementation group: 'org.postgresql', name: 'postgresql', version: '42.2.20'
  implementation group: 'org.flywaydb', name: 'flyway-core', version: '7.9.0'

  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap', version: '3.0.3'
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-netflix-hystrix', version: '2.2.8.RELEASE'

  implementation group: 'io.springfox', name: 'springfox-swagger2', version: versions.springfoxSwagger
  implementation group: 'io.springfox', name: 'springfox-swagger-ui', version: versions.springfoxSwagger

  implementation group: 'uk.gov.hmcts.reform', name: 'logging', version: versions.reformLogging
  implementation group: 'uk.gov.hmcts.reform', name: 'logging-appinsights', version: versions.reformLogging
  implementation group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: '4.0.0'
  implementation group: 'uk.gov.hmcts.reform', name: 'idam-client', version: '2.0.0'
  implementation group: 'uk.gov.hmcts.reform', name: 'core-case-data-store-client', version: '4.7.6'

  implementation group: 'com.azure', name: 'azure-messaging-servicebus', version: '7.2.3'

  implementation group: 'io.vavr', name: 'vavr', version: '0.10.4'
  implementation group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '3.0.3'

  annotationProcessor group: 'org.springframework.boot', name: 'spring-boot-configuration-processor'

  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }
  testImplementation group: 'com.typesafe', name: 'config', version: '1.4.1'
  testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.11.2'
  testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.20.2'
  testImplementation group: 'org.awaitility', name: 'awaitility', version: '4.1.0'
  testImplementation group: 'io.rest-assured', name: 'rest-assured'

  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: versions.junit
  testImplementation group: 'org.junit.platform', name: 'junit-platform-commons', version: versions.junitPlatform
  testImplementation group: 'org.junit.platform', name: 'junit-platform-engine', version: versions.junitPlatform
  testImplementation group: 'io.github.netmikey.logunit', name: 'logunit-core', version: '1.1.0'
  testImplementation group: 'com.github.hmcts', name: 'fortify-client', version: '1.2.1', classifier: 'all'
  testRuntimeOnly group: 'io.github.netmikey.logunit', name: 'logunit-logback', version: '1.1.0'

  integrationTestImplementation sourceSets.main.runtimeClasspath
  integrationTestImplementation sourceSets.test.runtimeClasspath
  integrationTestImplementation group: 'org.springframework.cloud', name: 'spring-cloud-contract-wiremock', version: '3.0.3'

  integrationTestImplementation group: 'com.github.tomakehurst', name: 'wiremock-jre8', version: '2.30.0'

  integrationTestImplementation group: 'org.testcontainers', name: 'postgresql', version: '1.16.0', {
    exclude group: 'junit', module: 'junit'
  }
  integrationTestImplementation group: 'org.testcontainers', name: 'junit-jupiter', version: '1.15.2'

  functionalTestImplementation sourceSets.main.runtimeClasspath
  functionalTestImplementation sourceSets.test.runtimeClasspath
  functionalTestImplementation group: 'uk.gov.hmcts.reform', name: 'document-management-client', version: '7.0.0'

  functionalTestImplementation group: 'org.testcontainers', name: 'postgresql', version: '1.16.0', {
    exclude group: 'junit', module: 'junit'
  }
  functionalTestImplementation group: 'org.testcontainers', name: 'junit-jupiter', version: '1.15.2'

  smokeTestImplementation sourceSets.main.runtimeClasspath
  smokeTestImplementation sourceSets.test.runtimeClasspath

  contractTestImplementation sourceSets.main.runtimeClasspath
  contractTestImplementation sourceSets.test.runtimeClasspath
  contractTestImplementation group: 'io.rest-assured', name: 'rest-assured'
  contractTestImplementation group: 'au.com.dius', name: 'pact-jvm-consumer-junit5', version: '4.0.10'
}

mainClassName = 'uk.gov.hmcts.reform.bulkscan.orchestrator.Application'

bootJar {
  getArchiveFileName().set(provider {
    'bulk-scan-orchestrator.jar'
  })

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

import org.flywaydb.gradle.task.FlywayMigrateTask

flyway {
  user = System.getenv('DB_USER')
  password = System.getenv('DB_PASSWORD')

  baselineOnMigrate = true
  baselineVersion = '000'
}

task migratePostgresDatabase(type: FlywayMigrateTask) {
  baselineOnMigrate = true
  if (project.hasProperty("dburl")) { // set by jenkins
    url = "jdbc:postgresql://${dburl}"
  }
}
